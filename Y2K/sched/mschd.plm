$title('MP/M II V2.0 Scheduler Transient Program')
sched:
do;

$include (copyrt.lit)
/*
  Revised:
    14 Sept 81  by Thomas Rolander
    27 Dec  04  by Hector Peraza (Y2K fixes)
*/

$include (proces.lit)
$include (queue.lit)
$include (xdos.lit)

/*
    Common Literals
*/

  declare true literally '0FFFFH';
  declare false literally '0';
  declare forever literally 'while true';
  declare boolean literally 'byte';

  declare fcb(1) byte external;

  declare start label;
  declare jmp$to$start structure (
    jmp$instr byte,
    jmp$location address ) data (
    0c3h,
    .start-3);

  mon1:
    procedure (func,info) external;
      declare func byte;
      declare info address;
    end mon1;

  mon2:
    procedure (func,info) byte external;
      declare func byte;
      declare info address;
    end mon2;

  mon2a:
    procedure (func,info) address external;
      declare func byte;
      declare info address;
    end mon2a;

  declare xdos literally 'mon2';
  declare xdosa literally 'mon2a';

  print$buffer:
    procedure (buffadr);
      declare buffadr address;
      call mon1 (9,buffadr);
    end print$buffer;

  system$reset:
    procedure;
      call mon1 (0,0);
    end system$reset;

  declare sched$uqcb userqcb
    initial (0,.new$entry,'Sched   ');

  declare ret address;  /* Warning: this is global */

  declare msg$adr address initial (.default$msg);
  declare default$msg (*) byte data (
    'Illegal time/date specification','$');


/*****************************************************

          Time & Date ASCII Conversion Code

 *****************************************************/

declare tod$adr address;
declare tod based tod$adr structure (
  opcode byte,
  date address,
  hrs byte,
  min byte,
  sec byte,
  ASCII (23) byte );

declare string$adr address;
declare string based string$adr (1) byte;
declare index byte;

declare lit literally 'literally',
  word lit 'address';

emitchar: procedure(c);
    declare c byte;
    string(index := index + 1) = c;
    end emitchar;

emitn: procedure(a);
    declare a address;
    declare c based a byte;
    do while c <> '$';
      string(index := index + 1) = c;
      a = a + 1;
    end;
    end emitn;


emit$bcd: procedure(b);
    declare b byte;
    call emitchar('0'+b);
    end emit$bcd;

emit$bcd$pair: procedure(b);
    declare b byte;
    call emit$bcd(shr(b,4));
    call emit$bcd(b and 0fh);
    end emit$bcd$pair;

emit$colon: procedure(b);
    declare b byte;
    call emit$bcd$pair(b);
    call emitchar(':');
    end emit$colon;

emit$bin$pair: procedure(b);
    declare b byte;
    call emit$bcd(b/10);
    call emit$bcd(b mod 10);
    end emit$bin$pair;

emit$year: procedure(b);
    declare b byte;
    declare y word;
    y = 1900 + b;
    call emit$bcd((y/1000) mod 10);
    call emit$bcd((y/100) mod 10);
    call emit$bcd((y/10) mod 10);
    call emit$bcd(y mod 10);
    end emit$year;

emit$slant: procedure(b);
    declare b byte;
    call emit$bin$pair(b);
    call emitchar('/');
    end emit$slant;

declare chr byte;

gnc: procedure;
    /* get next command byte */
    if chr = 0 then return;
    if index = 20 then
    do;
      chr = 0;
      return;
    end;
    chr = string(index := index + 1);
    end gnc;

deblank: procedure;
        do while chr = ' ';
        call gnc;
        end;
    end deblank;

numeric: procedure byte;
    /* test for numeric */
    return (chr - '0') < 10;
    end numeric;

scan$numeric: procedure(lw,uw) word;
    declare (lw,uw) word;
    declare w word;
    w = 0;
    call deblank;
    if not numeric then go to error;
        do while numeric;
        if (w and 1110$0000$0000$0000b) <> 0 then go to error;
        w = shl(w,3) + shl(w,1); /* w = w * 10 */
        if carry then go to error;
        w = w + (chr - '0');
        if carry then go to error;
        call gnc;
        end;
    if (w < lw) or (w > uw) then go to error;
    return w;
    end scan$numeric;

scan$delimiter: procedure(d,lb,ub) byte;
    declare (d,lb,ub) byte;
    call deblank;
    if chr <> d then go to error;
    call gnc;
    return scan$numeric(lb,ub);
    end scan$delimiter;

declare
    base$year lit '78',   /* base year for computations */
    base$day  lit '0',    /* starting day for base$year 0..6 */
    month$size (*) byte data
    /* jan feb mar apr may jun jul aug sep oct nov dec */
    (   31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31),
    month$days (*) word data
    /* jan feb mar apr may jun jul aug sep oct nov dec */
    (  000,031,059,090,120,151,181,212,243,273,304,334);

leap$days: procedure(y,m) byte;
    declare (y,m) byte;
    /* compute days accumulated by leap years */
    declare yp byte;
    yp = shr(y,2); /* yp = y/4 */
    if (y and 11b) = 0 and month$days(m) < 59 then
        /* y not 00, y mod 4 = 0, before march, so not leap yr */
        return yp - 1;
    /* otherwise, yp is the number of accumulated leap days */
    return yp;
    end leap$days;

declare word$value word;

get$next$digit: procedure byte;
    /* get next lsd from word$value */
    declare lsd byte;
    lsd = word$value mod 10;
    word$value = word$value / 10;
    return lsd;
    end get$next$digit;

bcd:
  procedure (val) byte;
    declare val byte;
    return shl((val/10),4) + val mod 10;
  end bcd;

declare (month, day, year, hrs, min, sec) byte;

set$date$time: procedure;
    declare (i, leap$flag) byte; /* temporaries */
    declare long$year word;
    month = scan$numeric(1,12) - 1;
    /* may be feb 29 */
    if (leap$flag := month = 1) then i = 29;
        else i = month$size(month);
    day   = scan$delimiter('/',1,i);
    call deblank;
    if chr <> '/' then go to error;
    call gnc;
    long$year  = scan$numeric(0,2200);
    if long$year > 1900 then
        long$year = long$year - 1900;
    year = long$year;
    if year < base$year then
        year = year + 100;
    /* ensure that feb 29 is in a leap year */
    if leap$flag and day = 29 and (year and 11b) <> 0 then
        /* feb 29 of non-leap year */ go to error;
    /* compute total days */
     tod.date = month$days(month)
                + 365 * (year - base$year)
                + day
                - leap$days(base$year,0)
                + leap$days(year,month);

    tod.hrs   = bcd (scan$numeric(0,23));
    tod.min   = bcd (scan$delimiter(':',0,59));
    if tod.opcode = 2 then
    /* date, hours and minutes only */
    do;
      if chr = ':'
        then i = scan$delimiter (':',0,59);
      tod.sec = 0;
    end;
    /* include seconds */
    else tod.sec   = bcd (scan$delimiter(':',0,59));

    end set$date$time;

bcd$pair: procedure(a,b) byte;
    declare (a,b) byte;
    return shl(a,4) or b;
    end bcd$pair;


compute$year: procedure;
    /* compute year from number of days in word$value */
    declare year$length word;
    year = base$year;
        do forever;
        year$length = 365;
        if (year and 11b) = 0 then /* leap year */
            year$length = 366;
        if word$value <= year$length then
            return;
        word$value = word$value - year$length;
        year = year + 1;
        end;
    end compute$year;

declare
    week$day  byte, /* day of week 0 ... 6 */
    day$list (*) byte data
    ('Sun$Mon$Tue$Wed$Thu$Fri$Sat$'),
    leap$bias byte; /* bias for feb 29 */

compute$month: procedure;
    month = 12;
        do while month > 0;
        if (month := month - 1) < 2 then /* jan or feb */
            leapbias = 0;
        if month$days(month) + leap$bias < word$value then return;
        end;
    end compute$month;

declare
    date$test byte,    /* true if testing date */
    test$value word;   /* sequential date value under test */

get$date$time: procedure;
    /* get date and time */
    hrs = tod.hrs;
    min = tod.min;
    sec = tod.sec;
    word$value = tod.date;
    /* word$value contains total number of days */
    week$day = (word$value + base$day - 1) mod 7;
    call compute$year;
    /* year has been set, word$value is remainder */
    leap$bias = 0;
    if (year and 11b) = 0 and word$value > 59 then
        /* after feb 29 on leap year */ leap$bias = 1;
    call compute$month;
    day = word$value - (month$days(month) + leap$bias);
    month = month + 1;
    end get$date$time;

emit$date$time: procedure;
    call emitn(.day$list(shl(week$day,2)));
    call emitchar(' ');
    call emit$slant(month);
    call emit$slant(day);
    call emit$year(year);
    call emitchar(' ');
    call emit$colon(hrs);
    call emit$colon(min);
    call emit$bcd$pair(sec);
    end emit$date$time;

tod$ASCII:
  procedure (parameter);
    declare parameter address;

    ret = 0;
    tod$adr = parameter;
    string$adr = .tod.ASCII;
    if tod.opcode = 0 then
    do;
      call get$date$time;
      index = -1;
      call emit$date$time;
    end;
    else
    do;
      if (tod.opcode = 1) or
         (tod.opcode = 2) then
      do;
        chr = string(index:=0);
        call set$date$time;
        ret = .string(index);
      end;
      else
      do;
        go to error;
      end;
    end;
  end tod$ASCII;

/********************************************************
 ********************************************************/


  declare new$entry structure (
    date address,
    hrs byte,
    min byte,
    cli$command (65) byte );

  declare lcltod structure (
    opcode byte,
    date address,
    hrs byte,
    min byte,
    sec byte,
    ASCII (23) byte ) at (.fcb(31));

  fill$entry:
    procedure;

      new$entry.cli$command(0) = shl (mon2 (25,0),4)
                                    + mon2 (32,0ffh);
      new$entry.cli$command(1) = mon2 (get$console$nmb,0);
      lcltod.opcode = 2;
      call tod$ASCII (.lcltod);
      if ret <> 0ffffh then
      do;
        new$entry.cli$command(64) = 0dh;
        ret = ret + 1;
        call move (63-(ret-.lcltod.min),ret,
                   .new$entry.cli$command(2));
        new$entry.date = lcltod.date;
        new$entry.hrs = lcltod.hrs;
        new$entry.min = lcltod.min;
      end;
      else
      do;
        go to error;
      end;
    end fill$entry;


  declare last$dseg$byte byte
    initial (0);

/*
  sched:
*/

start:
do;
  if xdos (open$queue,.sched$uqcb) = 0ffh then
  do;
    msgadr = .('Resident portion of scheduler is not in memory','$');
    go to error;
  end;
  call fill$entry;
  if xdos (cond$write$queue,.sched$uqcb) = 0ffh then
  do;
    msg$adr = .('Scheduler queue is full','$');
    go to error;
  end;
  call system$reset;
end;

error:
do;
  call print$buffer (msg$adr);
  call system$reset;
end;

end sched;
